plugins {
    id("com.github.johnrengelman.shadow") version "6.0.0"
}

dependencies {
    // Pull in the New Relic OpenTelemetry exports artifacts so that they can get shadowed into the agent jar
    implementation("com.newrelic.telemetry:opentelemetry-exporters-newrelic:${versions.opentelemetryExportersNewRelic}")
    implementation("com.newrelic.telemetry:opentelemetry-exporters-newrelic-auto:${versions.opentelemetryExportersNewRelic}")
}

tasks {
    shadowJar {
        mergeServiceFiles()

        exclude("**/module-info.class")

        // Prevents conflict with other SLF4J instances. Important for premain.
        relocate("org.slf4j", "io.opentelemetry.javaagent.slf4j")
        // rewrite dependencies calling Logger.getLogger
        relocate("java.util.logging.Logger", "io.opentelemetry.javaagent.bootstrap.PatchLogger")

        // prevents conflict with library instrumentation
        relocate("io.opentelemetry.instrumentation.api", "io.opentelemetry.javaagent.shaded.instrumentation.api")

        // relocate OpenTelemetry API
        relocate("io.opentelemetry.api", "io.opentelemetry.javaagent.shaded.io.opentelemetry.api")
        relocate("io.opentelemetry.context", "io.opentelemetry.javaagent.shaded.io.opentelemetry.context")

        // relocate OpenTelemetry API dependency
        relocate("io.grpc", "io.opentelemetry.javaagent.shaded.io.grpc")
    }
}